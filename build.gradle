buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '0.8'
    }
}

ext {
    projectDescription = 'A server administration suite from The Voxel Box'
    url = 'http://voxelwiki.com/minecraft/VoxelGuest'
}

apply plugin: 'shadow'
apply plugin: 'java'
apply plugin: 'checkstyle'

archivesBaseName = 'VoxelGuest'
group = 'com.thevoxelbox'
version = baseVersion

if (Boolean.valueOf(isSnapshot))
    version += '-SNAPSHOT'
if (System.getenv().containsKey('BUILD_NUMBER'))
    version += "-jnks${System.getenv('BUILD_NUMBER')}"

println 'Building VoxelGuest version ' + version

sourceCompatibility = 1.7

repositories {
    mavenCentral()

    maven {
        name 'Bukkit'
        url 'http://repo.bukkit.org/content/groups/public'
    }

    maven {
        name 'Vault'
        url 'http://ci.herocraftonline.com/plugin/repository/everything'
    }

    maven {
        name 'MCstats'
        url 'http://repo.mcstats.org/content/repositories/public'
    }
}

dependencies {
    compile group: 'org.bukkit', name: 'bukkit', version: '1.7.2-R0.2-SNAPSHOT'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.2.4'
    compile group: 'com.google.guava', name: 'guava', version: '10.0'
    compile group: 'net.milkbowl.vault', name: 'Vault', version: '1.2.27', transitive: false
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'com.j256.ormlite', name: 'ormlite-core', version: '4.47'
    compile group: 'com.j256.ormlite', name: 'ormlite-jdbc', version: '4.47'
    runtime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.7.2'
    compile group: 'org.mcstats.bukkit', name: 'metrics', version: 'R7'
    compile group: 'args4j', name: 'args4j', version: '2.0.26'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.9-RC1'

    compile group: 'com.intellij', name: 'annotations', version: '12.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {
    filter {
        line ->
            line
                    .replace('${project.name}', rootProject.name)
                    .replace('${project.build.version}', version)
                    .replace('${project.url}', url)
                    .replace('${project.description}', projectDescription)
    }
}

shadow {
    filter('org.bukkit:bukkit') {
        exclude '**'
    }

    filter('com.google.guava:guava') {
        exclude '**'
    }

    filter('org.xerial') {
        exclude '**'
    }

    filter('net.milkbowl.vault:Vault') {
        exclude '**'
    }

    filter('org.apache.log4j:log4j') {
        exclude '**'
    }

    relocation {
        pattern = 'com.google.gson'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.com.google.gson'
    }

    relocation {
        pattern = 'javax'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.javax'
    }

    relocation {
        pattern = 'javassist'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.javassist'
    }

    relocation {
        pattern = 'org'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org'
    }

    relocation {
        pattern = 'com.j256.ormlite'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.com.j256.ormlite'
    }

    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'license/*'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '0.8'
      //  classpath 'net.monofraps:GradleBukkit:1.0'
    }
}

ext {
    projectDescription = 'A server administration suite from The Voxel Box'
    url = 'http://voxelwiki.com/minecraft/VoxelGuest'
}

apply plugin: 'shadow'
apply plugin: 'java'
apply plugin: 'checkstyle'
//apply plugin: 'gradle-bukkit'

archivesBaseName = 'VoxelGuest'
group = 'com.thevoxelbox'
version = '5.2.0'
sourceCompatibility = 1.7

gradle.taskGraph.whenReady {taskGraph ->
    if(!taskGraph.hasTask(release)) {
        version += '-SNAPSHOT'
    }
}
if (System.getenv().containsKey('BUILD_NUMBER'))
    version += "-jnks${System.getenv('BUILD_NUMBER')}"

println 'Building VoxelGuest version ' + version


/*bukkit {
    plugin 'build/distributions/VoxelGuest-5.2.0-SNAPSHOT-shadow.jar'
    remoteDebugging {}
}       */

repositories {
    mavenCentral()

    maven {
        name 'Bukkit'
        url 'http://repo.bukkit.org/content/groups/public'
    }

    maven {
        name 'Vault'
        url 'http://ci.herocraftonline.com/plugin/repository/everything'
    }

    maven {
        name 'MCstats'
        url 'http://repo.mcstats.org/content/repositories/public'
    }
}

dependencies {
    compile 'org.bukkit:bukkit:1.7.2-R0.2-SNAPSHOT'

    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.guava:guava:10.0'

    compile 'log4j:log4j:1.2.17'
    compile 'com.j256.ormlite:ormlite-core:4.47'
    compile 'com.j256.ormlite:ormlite-jdbc:4.47'

    compile 'args4j:args4j:2.0.26'
    compile 'org.reflections:reflections:0.9.9-RC1'

    compile 'org.apache.httpcomponents:httpcore:4.3.1'

    compile 'com.intellij:annotations:12.0'

    compile group: 'net.milkbowl.vault', name: 'Vault', version: '1.2.27', transitive: false
    compile 'org.mcstats.bukkit:metrics:R7'
    //runtime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.7.2'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {
    filter {
        line ->
            line
                    .replace('${project.name}', rootProject.name)
                    .replace('${project.build.version}', version)
                    .replace('${project.url}', url)
                    .replace('${project.description}', projectDescription)
    }
}

shadow {
    //temporary
    filter('org.apache.httpcomponents:httpcore') {
        exclude '**'
    }

    filter('org.bukkit:bukkit') {
        exclude '**'
    }

    filter('com.google.guava:guava') {
        exclude '**'
    }

    filter('org.xerial') {
        exclude '**'
    }

    filter('net.milkbowl.vault:Vault') {
        exclude '**'
    }

    filter('org.apache.log4j:log4j') {
        exclude '**'
    }

    relocation {
        pattern = 'com.google.gson'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.com.google.gson'
    }

    relocation {
        pattern = 'javax'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.javax'
    }

    relocation {
        pattern = 'javassist'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.javassist'
    }

    relocation {
        pattern = 'org.apache'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.apache'
        excludes = ['org.apache.commons.lang.*']
    }

    relocation {
        pattern = 'org.dom4j'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.dom4j'
    }

    relocation {
        pattern = 'org.jetbrains'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.jetbrains'
    }

    relocation {
        pattern = 'org.intellij'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.intellij'
    }

    relocation {
        pattern = 'org.kohsuke'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.kohsuke'
    }

    relocation {
        pattern = 'org.mcstats'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.mcstats'
    }

    relocation {
        pattern = 'org.reflections'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.reflections'
    }

    relocation {
        pattern = 'org.w3c'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.w3c'
    }

    relocation {
        pattern = 'org.xml'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.org.xml'
    }

    relocation {
        pattern = 'com.j256.ormlite'
        shadedPattern = 'com.thevoxelbox.voxelguest.libs.com.j256.ormlite'
    }

    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'license/*'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

task release(dependsOn: 'build') << {
    println "Building release!"
}
